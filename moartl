#!/usr/bin/env perl6
# Copyright 2016 cygx <cygx@cpan.org>
# Distributed under the Boost Software License, Version 1.0

use v6;

class Op {
    has @.signature;
    has $.suffix;
    method suffix { $!suffix ?? "_{$!suffix}" !! '' }
    method arity { +@!signature }
    method eval($op, *@args) {
        my $i = 0;
        "    {$op}{$.suffix}" ~ @*made.map: -> $arg {
            ' ' ~ do given @!signature[$i++] {
                when 'i' { $arg.eval }
                when 's' { $arg.eval }
                when 'I' { $arg.promote.eval }
                when 'S' { $arg.promote.eval }
            }
        }
    }
}

sub op(*@signature, :$suffix) {
    Op.new(:@signature, :$suffix);
}

my @ops = <dec exit print say>;
my constant OPS = {
    dec => op(<I>, :suffix<i>),
    exit => op(<I>),
    print => op(<S>),
    say => op(<S>),
}

my $lines;
my $line;
my $n;

my @scopes;
my $blocks;

class X::MoarTL::Syntax is Exception {
    has $.msg;
    has $.n;
    has $.line;
    method new($msg) { self.bless(:$msg, :$n, :$line) }
    method message { "syntax error: $!msg\n[$!n] $!line" }
}

sub parse { ... }

proto MAIN(|) {
    CATCH {
        put ".abort {.^name}";
        when X::MoarTL::Syntax { note .message }
        default { note $_ }
        exit 1;
    }

    {*}

    put '.done';
}

multi MAIN(Str $src) { parse $src }
multi MAIN(*@src where $_ > 1) { .&parse for @src }

class Block { ... }
class Var { ... }
class Tmp { ... }
class IVal { ... }
class SVal { ... }
class Noop { ... }
class Const { ... }
class Cast { ... }

sub bailout($msg = '?') is hidden-from-backtrace {
    die X::MoarTL::Syntax.new($msg);
}

sub next-line {
    my $next := $lines.pull-one;
    return $next if $next =:= IterationEnd;
    ++$n;
    $line = $next.trim;
}

sub lookup($name) {
    for @scopes {
        return .{$name}
            if .{$name}:exists;
    }

    Nil;
}

sub block($name) { ... }

sub parse($src --> Nil) {
    put ".file $src";
    put ".hll tiny";

    my %*scope;

    $n = 0;
    $lines := $src.IO.lines(:close).iterator();
    @scopes := [ %*scope ];

    while ($_ := next-line) !=:= IterationEnd { /^[
        || [\#|$]
        || (:s fn (\w+)${ put ".frame $0" })
        || (:s fn (\w+)'()' '{'${
            my $name = ~$0;
            put ".frame $name";
            $blocks = 0;
            block $name;
        })
        || {bailout}
    ]/ }
}

sub iv(Int() $i) { IVal.new(:$i) }
sub sv(Str() $s) { SVal.new(:$s) }
sub const($value) { Const.new(:$value) }
sub cast($expr, $type) { Cast.new(:$expr, :$type) }

my token subexpression {
    | ((\w+) <?{ lookup(~$0) ~~ Var }> { push @*made, lookup(~$0) })
    | ((\d+) { push @*made, iv(~$0) })
    | ("'" (<-[']>*) "'" { push @*made, sv(~$0) })
}

my token expression {
    | ((str) '(' <&subexpression> ')' { push @*made, cast(@*made.pop, ~$0) })
    | <&subexpression>
}

sub block($name) {
    my %*scope;
    my $*block = Block.new(:$name, id => $blocks++);
    %*scope{$name} = $*block;
    @scopes.unshift(%*scope);
    LEAVE @scopes.shift;

    put ".{$*block.bra}:";

    my @*made;
    while ($_ := next-line) !=:= IterationEnd { /^[
        | [\#|$]
        | (:s '.'(\w+) '{'${ block ~$0 })
        | ('}' ${
            put ".{$*block.ket}:";
            return;
        })
        | (:s (int) (\w+) '=' (\d+)${
            my ($type, $name, $value) = $/>>.Str;
            my $init = const(iv($value));
            my $var = Var.new(:$name, :$type, :$*block, :$init);
            (%*scope{$name} = $var).declare;
        })
        | (:s next (\w+) if <expression>${
            put "    if_i {@*made[0].eval} \@.{$*block.bra}";
            @*made = ();
        })
        | (:s (@ops) <expression>**{OPS{$0}.arity}%[<.ws>?','<.ws>?]${
            put OPS{~$0}.eval(~$0, @*made);
            @*made = ();
        })
        || {bailout}
    ]/ }

    bailout 'unclosed block';
}

sub suffix($_) {
    when 'int' { 'i' }
    when 'str' { 's' }
}

sub extsuffix($_) {
    when 'int' { 'i64' }
    when 'str' { 's' }
}

class Block {
    has $.name;
    has $.id;
    has %.temps;
    method type { 'block' }
    method bra { "bra{$!id}_{$!name}" }
    method ket { "ket{$!id}_{$!name}" }
}

role Alias {
    has $.type;
    has $.block;
    has $.init;
    has $!declared;
    method longname { ... }
    method declare {
        $!declared = True;
        put "    .local[{$.type}] {$.longname}";
        put $!init.eval("\@{$.longname}");
    }
    method eval {
        self.declare unless $!declared;
        "\@{$.longname}";
    }
}

class Var does Alias {
    has $.name;
    method longname { "var{$!block.id}_{$!name}" }
    method promote { self }
}

class Tmp does Alias {
    has $.id;
    submethod TWEAK { $!id = $!block.temps{$!type}++ }
    method longname { "tmp{$!block.id}_{suffix $.type}{$!id}" }
    method promote { self }
}

role Value {
    method type { ... }
    method eval { ... }
    method promote { Tmp.new(:$.type, :$*block, init => const(self)) }
}

class IVal does Value {
    has $.i;
    method type { 'int' }
    method eval { "$!i" }
}

class SVal does Value {
    has $.s;
    method type { 'str' }
    method eval { "'{$.enc}'" }
    sub enc($_) {
        when ^0x100 { .fmt("\\%02X") }
        when ^0x10000 { .fmt("\\u%04X") }
        when ^0x110000 { .fmt("\\U%06X") }
    }
    method enc { $!s.subst(:g, /\'|\v/, { .Str.NFC.map(&enc).join }) }
}

class Noop {
    method eval {}
}

class Const {
    has $.value;
    method eval($target) {
        "    const_{extsuffix $!value.type} {$target} {$!value.eval}";
    }
}

class Cast {
    has $.expr;
    has $.type;
    method promote { Tmp.new(:$!type, :$*block, init => self) }
    method eval($target) {
        "    coerce_{suffix $!expr.type}{suffix $!type} {$target} {$!expr.eval}";
    }
}
